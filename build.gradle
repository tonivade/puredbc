plugins {
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id "com.diffplug.oomph.ide" version "3.32.1"
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply plugin: 'com.diffplug.eclipse.apt'

group = 'com.github.tonivade'
version = '0.3-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  withJavadocJar()
  withSourcesJar()
}

jacoco {
  toolVersion = '0.8.6'
}

repositories {
  mavenLocal() 
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

test {
  useJUnitPlatform()
}

dependencies {
  annotationProcessor 'com.github.tonivade:purefun-processor:3.0'
  api 'com.github.tonivade:purefun-free:3.0'
  implementation 'com.github.tonivade:purefun-effect:3.0'
  runtimeOnly 'com.github.tonivade:purefun-instances:3.0'
  implementation 'io.r2dbc:r2dbc-spi:0.8.5.RELEASE'
  implementation 'io.r2dbc:r2dbc-pool:0.8.7.RELEASE'
  testRuntimeOnly 'com.h2database:h2:1.4.200'
  testRuntimeOnly 'io.r2dbc:r2dbc-h2:0.8.4.RELEASE'
  testImplementation 'com.zaxxer:HikariCP:5.0.0'
  testImplementation 'com.github.tonivade:purecheck:0.2-SNAPSHOT'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
  testImplementation 'org.junit.platform:junit-platform-runner:1.7.2'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      
      from components.java

      pom {
        name = 'PureDBC'
        description = 'Pure Functional Database Connection Layer'
        url = 'https://github.com/tonivade/puredbc'
        inceptionYear = '2020'

        scm {
          url = 'https://github.com/tonivade/puredbc'
          connection = 'scm:https://github.com/tonivade/puredbc.git'
          developerConnection = 'scm:git://github.com/tonivade/puredbc.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://github.com/tonivade/puredbc/blob/master/LICENSE'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'tonivade'
            name = 'Antonio Mu√±oz'
            email = 'antoniogmc@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusPublishing {
  repositories {
    sonatype {

      stagingProfileId = '19a96b937cdb5'

      username = project.findProperty('nexusUsername')
      password = project.findProperty('nexusPassword')
    }
  }

  clientTimeout = Duration.ofSeconds(300)
  connectTimeout = Duration.ofSeconds(60)

  transitionCheckOptions {
    maxRetries.set(40)
    delayBetween.set(java.time.Duration.ofMillis(3000))
  }
}
