plugins {
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id "com.diffplug.oomph.ide" version "3.36.1"
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply plugin: 'com.diffplug.eclipse.apt'

group = 'com.github.tonivade'
version = '0.4-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

jacoco {
  toolVersion = '0.8.7'
}

repositories {
  mavenLocal() 
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
  
  withJavadocJar()
  withSourcesJar()
}

compileJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 17
}

compileTestJava {
  options.compilerArgs << '-Xlint:unchecked'
  options.compilerArgs << '-Xlint:rawtypes'
  options.release = 17
}

test {
  useJUnitPlatform()

  reports.html.required = false
}

dependencies {
  annotationProcessor 'com.github.tonivade:purefun-processor:3.99.0-SNAPSHOT'
  api 'com.github.tonivade:purefun-free:3.99.0-SNAPSHOT'
  implementation 'com.github.tonivade:purefun-effect:3.99.0-SNAPSHOT'
  runtimeOnly 'com.github.tonivade:purefun-instances:3.99.0-SNAPSHOT'
  implementation 'io.r2dbc:r2dbc-spi:0.9.1.RELEASE'
  implementation 'io.r2dbc:r2dbc-pool:0.9.0.RELEASE'
  testRuntimeOnly 'com.h2database:h2:2.1.212'
  testRuntimeOnly 'io.r2dbc:r2dbc-h2:0.9.1.RELEASE'
  testImplementation 'com.zaxxer:HikariCP:5.0.1'
  testImplementation 'com.github.tonivade:purecheck:0.3-SNAPSHOT'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
  testImplementation 'org.junit.platform:junit-platform-runner:1.8.2'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      
      from components.java

      pom {
        name = 'PureDBC'
        description = 'Pure Functional Database Connection Layer'
        url = 'https://github.com/tonivade/puredbc'
        inceptionYear = '2020'

        scm {
          url = 'https://github.com/tonivade/puredbc'
          connection = 'scm:https://github.com/tonivade/puredbc.git'
          developerConnection = 'scm:git://github.com/tonivade/puredbc.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://github.com/tonivade/puredbc/blob/master/LICENSE'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'tonivade'
            name = 'Antonio MuÃ±oz'
            email = 'antoniogmc@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusPublishing {
  repositories {
    sonatype {

      stagingProfileId = '19a96b937cdb5'

      username = project.findProperty('nexusUsername')
      password = project.findProperty('nexusPassword')
    }
  }

  clientTimeout = Duration.ofSeconds(300)
  connectTimeout = Duration.ofSeconds(60)

  transitionCheckOptions {
    maxRetries.set(40)
    delayBetween.set(java.time.Duration.ofMillis(3000))
  }
}
